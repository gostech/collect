import com.android.ddmlib.DdmPreferences

/**************************************************************************************************
 GRADLE CONFIG
 *************************************************************************************************/

if ((new File(gradle.local)).exists()) {
    apply from: gradle.local
} else {
    apply from: gradle.remote
}

applicationScripts.each {
    apply from: it
}

apply plugin: 'com.android.application'

DdmPreferences.setTimeOut(60000)

android {
    // The following are set in the common.gradle file
    compileSdkVersion(28)
    buildToolsVersion('28.0.3')

    defaultConfig {
        applicationId("uk.co.bioriskltd.odkcollect")
        minSdkVersion(26)
        targetSdkVersion(28)
        versionCode(1057)
        versionName("1.0.0")
    }

    buildTypes {
        release {
            minifyEnabled(minify)
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            testCoverageEnabled(testCoverage) // Allows AndroidTest JaCoCo reports to be generated
        }
    }

    packagingOptions {
        // Pick first occurrence of any files that cause conflicts, as defined in common.gradle
        packageList.each {
            pickFirst it
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

configurations.all {
    resolutionStrategy {
        cacheDynamicVersionsFor(0, 'seconds')
        cacheChangingModulesFor(0, 'seconds')
    }
    transitive = true
}

dependencies {
    implementation fileTree(include: '*.jar', dir: 'libs')
    if (playServicesPath.exists() && gradle.useLocal) { // Local project is favoured
        implementation project(playServicesName)
    } else {
        implementation(group: groupId, name: playServicesArtifactName,
                version: (useLatestPlayServices ? latestVersion : playServicesSnapshot), ext: 'aar')
    }
    // Testing-only dependencies
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support:support-v13:28.0.0'
    androidTestImplementation 'com.android.support:support-v4:28.0.0'
    androidTestImplementation 'junit:junit:4.12'
    implementation group: 'cz.msebera.android', name: 'httpclient', version: '4.4.1.2'
    implementation 'com.android.support:support-core-utils:28.0.0'
    implementation 'com.android.support:support-fragment:28.0.0'
    testImplementation 'junit:junit:4.12'
}
